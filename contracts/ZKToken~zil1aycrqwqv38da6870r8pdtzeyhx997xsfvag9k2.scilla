scilla_version 0

(* This contract implements a fungible token interface a la ERC20.*)

(***************************************************)
(*               Associated library                *)
(***************************************************)
library ZKToken

let one_msg =
  fun (msg : Message) =>
  let nil_msg = Nil {Message} in
Cons {Message} msg nil_msg

let min_int =
  fun (a : Uint128) => fun (b : Uint128) =>
  let alt = builtin lt a b in
  match alt with
  | True =>
    a
  | False =>
    b
  end

let le_int =
  fun (a : Uint128) => fun (b : Uint128) =>
    let x = builtin lt a b in
    match x with
    | True => True
    | False =>
      let y = builtin eq a b in
      match y with
      | True => True
      | False => False
      end
    end

let zero_address = 0x0000000000000000000000000000000000000000    

(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract ZKToken
(owner : ByStr20,
 total_tokens : Uint128,
 decimals : Uint32,
 name : String,
 symbol : String)

(* Initial balance is not stated explicitly: it's initialized when creating the contract. *)

field balances : Map ByStr20 Uint128 =
  let m = Emp ByStr20 Uint128 in
    builtin put m owner total_tokens
field allowed : Map ByStr20 (Map ByStr20 Uint128) = Emp ByStr20 (Map ByStr20 Uint128)

field is_init : Bool =  False 



transition init ()
  is_owner = builtin eq owner _sender;
  match is_owner with
  | False =>
    e = {_eventname : "InitError";msg :"not owner"};
    event e
  | True =>
    inited <- is_init;
    match inited with
    | False =>
        trueVal = True ;
        is_init := trueVal ;
        et = {_eventname : "Transfer"; from : zero_address; to : owner; value : total_tokens};
        event et;
        e = {_eventname : "InitOK";msg :"success" };
        event e
    | True =>
        e = {_eventname : "initError";msg :"inited before" };
        event e
    end
  end
end


transition balanceOf (tokenOwner : ByStr20)
  bal <- balances[tokenOwner];
  match bal with
  | Some v =>
	e = {_eventname : "BalanceOf"; address : tokenOwner; balance : v};
	event e
  | None =>
	e = {_eventname : "BalanceOf"; address : tokenOwner; balance : Uint128 0};
    event e
  end
end

transition totalSupply ()
  e = {_eventname : "TotalSupply"; caller : _sender; balance : total_tokens};
  event e
end

transition transfer (to : ByStr20, tokens : Uint128)
  bal <- balances[_sender];
  match bal with
  | Some b =>
    can_do = le_int tokens b;
    match can_do with
    | True =>
      (* subtract tokens from _sender and add it to "to" *)
      new_sender_bal = builtin sub b tokens;
      balances[_sender] := new_sender_bal;

      (* Adds tokens to "to" address *)
      to_bal <- balances[to];
      new_to_bal = match to_bal with
      | Some x => builtin add x tokens
      | None => tokens
      end;

  	  balances[to] := new_to_bal;
      et = {_eventname : "Transfer"; from : _sender; to : to; value : tokens};
      event et
    | False =>
      (* balance not sufficient. *)
      e = {_eventname : "TransferFailure"; sender : _sender; recipient : to; amount : Uint128 0};
      event e
    end
  | None =>
    (* no balance record, can't transfer *)
  	e = {_eventname : "TransferFailure"; sender : _sender; recipient : to; amount : Uint128 0};
    event e
  end
end

transition transferFrom (from : ByStr20, to : ByStr20, tokens : Uint128)
  bal <- balances[from];
  (* Check if _sender has been authorized by "from" *)
  sender_allowed_from <- allowed[from][_sender];
  match bal with
  | Some a =>
    match sender_allowed_from with
    | Some b =>
        (* We can only transfer the minimum of available or authorized tokens *)
        t = min_int a b;
        can_do = le_int tokens t;
        match can_do with
        | True =>
            (* tokens is what we should subtract from "from" and add to "to" *)
            new_from_bal = builtin sub a tokens;
            balances[from] := new_from_bal;
            to_bal <- balances[to];
            match to_bal with
            | Some tb =>
                new_to_bal = builtin add tb tokens;
                balances[to] := new_to_bal
            | None =>
                (* "to" has no balance. So just set it to tokens *)
                balances[to] := tokens
            end;
            (* reduce "allowed" by "tokens" *)
            new_allowed = builtin sub b tokens;
            allowed[from][_sender] := new_allowed;
            et = {_eventname : "Transfer"; from : from; to : to; value : tokens};
            event et;
            msg = {_tag: "TransferFromSuccessResponse";_recipient: _sender;_amount: Uint128 0; from:from;to:to;value:tokens };
            msgs = one_msg msg;
            send msgs
        | False =>
            e = {_eventname : "TransferFromFailure"; sender : from; recipient : to; amount : Uint128 0};
            event e;
            msg = {_tag: "TransferFromFailureResponse";_recipient: _sender;_amount: Uint128 0; from:from;to:to;value:tokens };
            msgs = one_msg msg;
            send msgs
        end
    | None =>
        e = {_eventname : "TransferFromFailure"; sender : from; recipient : to; amount : Uint128 0};
        event e;
        msg = {_tag: "TransferFromFailureResponse";_recipient: _sender;_amount: Uint128 0; from:from;to:to;value:tokens };
        msgs = one_msg msg;
        send msgs
    end
  | None =>
	e = {_eventname : "TransferFromFailure"; sender : from; recipient : to; amount : Uint128 0};
	event e;
  msg = {_tag: "TransferFromFailureResponse";_recipient: _sender;_amount: Uint128 0; from:from;to:to;value:tokens };
  msgs = one_msg msg;
  send msgs
  end
end

transition approve (spender : ByStr20, tokens : Uint128)
  allowed[_sender][spender] := tokens;
  e = {_eventname : "Approve"; approver : _sender; spender : spender; amount : tokens};
  event e
end

transition allowance (tokenOwner : ByStr20, spender : ByStr20)
  spender_allowance <- allowed[tokenOwner][spender];
  match spender_allowance with
  | Some n =>
      e = {_eventname : "Allowance"; owner : tokenOwner; spender : spender; amount : n};
      event e
  | None =>
      e = {_eventname : "Allowance"; owner : tokenOwner; spender : spender; amount : Uint128 0};
      event e
  end
end

